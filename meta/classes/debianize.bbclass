# This software is a part of ISAR.
# Copyright (C) 2017-2019 Siemens AG
#
# SPDX-License-Identifier: MIT

CHANGELOG_V ?= "${PV}"
DPKG_ARCH ??= "any"
DEBIAN_DEPENDS ?= ""
DESCRIPTION ?= "must not be empty"
MAINTAINER ?= "Unknown maintainer <unknown@example.com>"

deb_add_changelog() {
	timestamp=$(find ${S}/ -type f -not -path "${S}/debian/*" -printf "%T@\n"|sort -n -r|head -n 1)
	if [ -n "${timestamp}" ]; then
		date=$(LANG=C date -R -d @${timestamp})
	else
		date=$(LANG=C date -R)
	fi
	cat <<EOF > ${S}/debian/changelog
${PN} (${CHANGELOG_V}) UNRELEASED; urgency=low

  * generated by Isar

 -- ${MAINTAINER}  ${date}
EOF
	if [ -f ${WORKDIR}/changelog ]; then
		if head -1 "${WORKDIR}"/changelog | \
			grep -q -e "^${PN} (${CHANGELOG_V})"
		then
			# entry for our version already there, use unmodified
			rm ${S}/debian/changelog
		else
			# prepend our entry to an existing changelog
			echo >> ${S}/debian/changelog
		fi
		cat ${WORKDIR}/changelog >> ${S}/debian/changelog
	fi
}

deb_create_compat() {
	echo 9 > ${S}/debian/compat
}

deb_create_control() {
	compat=$( cat ${S}/debian/compat )
	cat << EOF > ${S}/debian/control
Source: ${PN}
Section: misc
Priority: optional
Standards-Version: 3.9.6
Maintainer: ${MAINTAINER}
Build-Depends: debhelper (>= ${compat})

Package: ${PN}
Architecture: ${DPKG_ARCH}
Depends: ${DEBIAN_DEPENDS}
Description: ${DESCRIPTION}
EOF
}

DH_FIXPERM_EXCLUSIONS = \
    "${@' '.join(['-X ' + x for x in \
                  (d.getVar('PRESERVE_PERMS', False) or '').split()])}"

deb_create_rules() {
	cat << EOF > ${S}/debian/rules
#!/usr/bin/make -f

override_dh_fixperms:
	dh_fixperms ${DH_FIXPERM_EXCLUSIONS}

%:
	dh \$@
EOF
	chmod +x ${S}/debian/rules
}

deb_debianize() {
	if [ -f ${WORKDIR}/compat ]; then
		install -v -m 644 ${WORKDIR}/compat ${S}/debian/compat
	else
		deb_create_compat
	fi
	if [ -f ${WORKDIR}/control ]; then
		install -v -m 644 ${WORKDIR}/control ${S}/debian/control
	else
		deb_create_control
	fi
	if [ -f ${WORKDIR}/rules ]; then
		install -v -m 755 ${WORKDIR}/rules ${S}/debian/rules
	else
		deb_create_rules
	fi
	deb_add_changelog

	for t in pre post
	do
		for a in inst rm
		do
			if [ -f ${WORKDIR}/${t}${a} ]; then
				install -v -m 755 ${WORKDIR}/${t}${a} \
					${S}/debian/${t}${a}
			fi
		done
	done
}
